plugins {
	id "architectury-plugin" version "3.+"
	id "dev.architectury.loom" version "1.1.+" apply false
	id "io.github.juuxel.loom-quiltflower" version "1.+" apply false
	id "com.github.johnrengelman.shadow" version "7.+" apply false
	id "maven-publish"
}

def targetJavaVersion = 17
architectury.minecraft = rootProject.minecraft_version

subprojects {
	apply plugin: "dev.architectury.loom"
	apply plugin: "io.github.juuxel.loom-quiltflower"

	archivesBaseName = "$rootProject.archives_base_name-$project.name"

	dependencies {
		minecraft "com.minecraft:minecraft:$rootProject.minecraft_version"
		mappings loom.layered {
			mappings "org.quiltmc:quilt-mappings:${minecraft_version}+build.${project.quilt_mappings}:intermediary-v2"
		}
	}
}

allprojects {
	apply plugin: "java"
	apply plugin: "architectury-plugin"
	apply plugin: "maven-publish"

	sourceCompatibility = JavaVersion.toVersion(targetJavaVersion)
	targetCompatibility = JavaVersion.toVersion(targetJavaVersion)

	version = project.mod_version
	group = project.maven_group

	tasks.withType(JavaCompile).forEach {
		it.options.encoding('UTF-8')
		it.options.deprecation(true)
		it.options.incremental(true)

		it.options.release.set(targetJavaVersion)
	}

	repositories {
		maven { url = 'https://maven.terraformersmc.com/releases/' }
		maven { url = 'https://maven.shedaniel.me/' }
		maven { url = 'https://jitpack.io' }
		maven { url = 'https://maven.quiltmc.org/repository/release/' }
	}

	java.withSourcesJar()

	jar {
		from('LICENSE')
	}

	wrapper {
		distributionType(Wrapper.DistributionType.BIN)
	}
}